# Snakemake workflow for performing several population genomic comparisons of population replicates across two different
# habitat categories and two different time periods. These units however can be changed to any categorical variable with
# two different types.

report: "report/workflow.rst"

include: "rules/common.smk"

include: "rules/ref.smk"
include: "rules/preprocessing.smk"
include: "rules/qc.smk"
include: "rules/mapping.smk"
include: "rules/angsd.smk"
include: "rules/thetas.smk"
include: "rules/linkage.smk"
include: "rules/pca.smk"
include: "rules/admixture.smk"
include: "rules/inbreeding.smk"

# Accumulate desired output files from config file
all_outputs = []

if config["analyses"]["pca_pcangsd"]:
	all_outputs.append(expand(results + "/pcangsd/{population}.cov",
		population=config["dataset"]))

if config["analyses"]["admix_ngsadmix"]:
	all_outputs.append(
		expand(
			results+"/ngsadmix/{population}_K{kvalue}.qopt",
			population=config["dataset"],
			kvalue=config["params"]["ngsadmix"]["kvalues"]
			)
		)

if config["analyses"]["thetas_angsd"]:
	all_outputs.append(expand(
		results+"/angsd/thetas/{population}.thetaWindows.pestPG",
		population=pop_list))

if config["analyses"]["heterozygosity_angsd"]:
	all_outputs.append(expand(
		results+"/angsd/thetas/{population}.thetaWindows.pestPG",
		population=samples.index))

if config["analyses"]["inbreedingCoeff_ngsf-hmm"]:
	all_outputs.append(expand(
		results+"/inbreeding/{population}.indF", population=pop_list))

if config["analyses"]["ROH_ngsf-hmm"]:
	all_outputs.append(expand(
		results+"/inbreeding/{population}.ibd", population=pop_list))

# master target rule
rule all:
    input: all_outputs